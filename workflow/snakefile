import collections
import csv

import numpy as np
import shutil

chr_lens = np.genfromtxt(
    'misc_data/genome/chr_lens.txt',
    usecols=[1],
    skip_header=1,
    dtype=int
)

class PhenotypeLoadingArg:
    def __init__(self, data_field_id, age, *, unit = 'binary', categorical_covars = []):
        self.data_field_id = data_field_id
        self.age = age
        self.unit = unit
        self.categorical_covars = categorical_covars

pheno_args = {
    'height': PhenotypeLoadingArg(
        '50',
        'first-available',
        unit = 'cm'
    ),
    'total_bilirubin': PhenotypeLoadingArg(
        '30840',
        'first-available',
        unit = 'umol/L'
    ),
    'education_score_england': PhenotypeLoadingArg(
        '26414',
        'init-assessment',
        unit = 'English IMD Units'
    ),
    'fluid_intelligence_score': PhenotypeLoadingArg(
        '20016',
        'first-available',
        unit = 'Correct Answers'
    ),
    'inpatient_ICD10_main_diagnoses' : PhenotypeLoadingArg(
        '41202',
        'tbd'
    ),
    'tower_rearranging_correct' : PhenotypeLoadingArg(
        '21004',
        'first-available',
        unit = 'Correct Answers'
    ),
    's_word_count_pilot' : PhenotypeLoadingArg(
        '10612',
        'init-assessment',
        unit = 'Words Produced'
    ),
    'white_blood_cell_count': PhenotypeLoadingArg(
        '30000',
        'first-available',
        unit = '10^9 cells/Liter',
        categorical_covars = [('white_blood_cell_count_device_id', '30003')]
    )
}

rule load_shared_covars:
    output:
        protected("traits/shared_covars/shared_covars.npy"),
        protected("traits/shared_covars/assessment_ages.npy"),
        protected("traits/shared_covars/README.txt"),
        protected("traits/shared_covars/covar_names.txt")
    resources:
        time='00:05:00'
    shell:
        "traits/load_shared_covars.py"

rule decompress_field:
    output:
        protected("main_dataset/extracted_data/{field_name}_{field_id}.txt"),
        protected("main_dataset/extracted_data/{field_name}_{field_id}_README.txt")
    resources:
        time='05:00:00'
    shell:
        "main_dataset/decompress_trait.py {wildcards.field_name} {wildcards.field_id} "

def input_fields_for_phenotype(wildcards):
    phenotype = wildcards.phenotype
    if phenotype in pheno_args:
        args = pheno_args[phenotype]
    else:
        raise ValueError(f"Couldn't identify phenotype {phenotype}")
    return [
        f"main_dataset/extracted_data/{wildcards.phenotype}_{args.data_field_id}.txt",
        *[f"main_dataset/extracted_data/{covar_field_name}_{covar_field_id}.txt" for
          covar_field_name, covar_field_id in args.categorical_covars]
    ]

rule load_phenotype:
    input:
        "traits/shared_covars/shared_covars.npy",
        input_fields_for_phenotype
    output:
        protected("traits/phenotypes/{phenotype}.npy"),
        protected("traits/phenotypes/{phenotype}_README.txt"),
        protected("traits/phenotypes/{phenotype}_unit.txt"),
        protected("traits/phenotypes/{phenotype}_covar_names.txt")
    resources:
        time='00:05:00'
    run:
        phenotype = wildcards.phenotype
        if phenotype in pheno_args:
            args = pheno_args[phenotype]
            assert args.unit != 'binary'
            base_command = (
                f"traits/load_trait_from_main_dataset.py "
                f"{phenotype} {args.data_field_id} '{args.unit}' {args.age} "
            )
            if not args.categorical_covars:
                shell(base_command)
            else:
                shell(
                    base_command + " --categorical-covars "
                    + " ".join(f'{name},{ID}' for name, ID in args.categorical_covars) + " "
                )
        else:
            raise ValueError(f"Couldn't identify phenotype {phenotype}")

rule plot_phenotype_by_sex:
    input:
        "traits/shared_covars/shared_covars.npy",
        "traits/phenotypes/{phenotype}.npy",
        "traits/phenotypes/{phenotype}_unit.txt"
    output:
        "traits/phenotypes/{phenotype}_distribution_by_sex.png"
    resources:
        time='24:00:00',
        threads=5,
        mem_gb=10
    shell:
        'traits/plot_phenotype.py {wildcards.phenotype} sex'

rule plot_phenotype_by_age:
    input:
        "traits/phenotypes/{phenotype}.npy",
        "traits/phenotypes/{phenotype}_unit.txt"
    output:
        "traits/phenotypes/{phenotype}_distribution_by_age.png"
    resources:
        time='24:00:00',
        threads=15,
        mem_gb=30
    shell:
        'traits/plot_phenotype.py {wildcards.phenotype} age'

rule subset_samples_for_phenotype:
    input:
        "traits/phenotypes/{phenotype}.npy",
        expand("sample_qc/common_filters/keep"),
        expand("sample_qc/common_filters/remove"),
        "misc_data/ukbgene/ukb46122_rel_s488282.dat"
    output:
        protected("sample_qc/runs/{phenotype}/README.txt"),
        protected("sample_qc/runs/{phenotype}/combined_unrelated.sample")
    shadow: 'minimal'
    resources:
        time='24:00:00'
    shell:
        'shadow_dir=$(pwd) ; cd $UKB ; export UKB=${{shadow_dir}} ; '
        'sample_qc/scripts/combine.py {wildcards.phenotype} && '
        'PHEN={wildcards.phenotype} sample_qc/scripts/test_combine.sh && '
        'sample_qc/scripts/unrelated_individuals.py {wildcards.phenotype} && '
        'sample_qc/scripts/verify_unrelatedness.py {wildcards.phenotype}'

rule rank_inverse_normalize_phenotype_subset:
    input:
        "traits/phenotypes/{phenotype}.npy",
        "sample_qc/runs/{phenotype}/combined_unrelated.sample"
    output:
        protected("traits/subset_rin_phenotypes/{phenotype}_README.txt"),
        protected("traits/subset_rin_phenotypes/{phenotype}.npy")
    resources:
        time='00:05:00'
    shell:
        'traits/rank_inverse_normalize.py {wildcards.phenotype}'

rule plot_str_locus:
    input:
        "traits/phenotypes/{phenotype}_unit.txt",
        'traits/phenotypes/{phenotype}.npy',
        'traits/subset_rin_phenotypes/{phenotype}.npy'
    output:
        'association/locus_plots/{phenotype}/{chrom}_{pos,[0-9]+}{transform,(_.*)?}.png',
        'association/locus_plots/{phenotype}/{chrom}_{pos,[0-9]+}{transform,(_.*)?}.csv'
    resources:
        time='00:30:00'
    run:
        if wildcards.transform == '':
            transform_step = 'original'
        elif wildcards.transform == '_rin':
            transform_step = 'rin'
        else:
            raise ValueError()
        shell(
            f'association/plot_loci.py {str_imputation_run_name} '
            f'{wildcards.phenotype} {transform_step} '
            f'{wildcards.chrom}:{wildcards.pos} '
        )

def concatenate_csvs(output_file, input_files):
    with open(output_file + '.temp', 'w') as outfile:
        first = True
        for f in input_files:
            with open(f) as infile:
                first_line = True
                for line in infile:
                    if first_line and first:
                        first = False
                        first_line = False
                        outfile.write(line)
                        continue
                    elif first_line and not first:
                        first_line = False
                        continue
                    outfile.write(line)
    shutil.move(output_file + '.temp', output_file)

def regions(region_len):
    regions = []
    for chrom in range(1, 23):
        chr_len = chr_lens[chrom-1]
        for start in range(1, chr_len, region_len):
            if start + region_len - 1 > chr_len:
                end = chr_len
            else:
                end = start + region_len - 1
            regions.append(f'{chrom}_{start}_{end}')
    return regions

rule write_my_imputed_snp_gwas_readme:
    output:
        protected('association/results/{phenotype}/my_imputed_snp/README.txt'),
        protected('association/results/{phenotype}/my_imputed_snp/time.stamp')
    resources:
        time='00:05:00'
    shell:
        'touch association/results/{wildcards.phenotype}/my_imputed_snp/time.stamp && '
        'association/my_regional_gwas.py imputed-snps {wildcards.phenotype} --readme'

rule run_regional_my_imputed_snp_gwas:
    input:
        "traits/phenotypes/{phenotype}.npy",
        'association/results/{phenotype}/my_imputed_snp/time.stamp',
        "traits/subset_rin_phenotypes/{phenotype}.npy"
    output:
        "association/results/{phenotype}/my_imputed_snp/batches/chr{chr}_{start}_{end}.tab"
    resources:
        time='12:00:00',
        mem_gb=9,
        threads=4
    shell:
        'association/my_regional_gwas.py imputed-snps {wildcards.phenotype} '
        '--region {wildcards.chr}:{wildcards.start}-{wildcards.end}'

rule concatenate_my_imputed_snp_gwas_chr21:
    input:
        lambda wildcards:
            [f'association/results/{wildcards.phenotype}/my_imputed_snp/batches/chr{region}.tab' for
             region in regions(int(1e5)) if region[:3] == '21_']
    output:
        protected('association/results/{phenotype}/my_imputed_snp/chr21_results.tab')
    resources:
        time='04:00:00'
    run:
        concatenate_csvs(output[0], input)

str_imputation_run_name = 'first_pass'

rule write_my_str_gwas_readme:
    output:
        protected('association/results/{phenotype}/my_str/README.txt'),
        protected('association/results/{phenotype}/my_str/time.stamp')
    resources:
        time='00:05:00'
    shell:
        'touch association/results/{wildcards.phenotype}/my_str/time.stamp && '
        'association/my_regional_gwas.py strs {wildcards.phenotype} --readme '
        f'--imputation-run-name {str_imputation_run_name} '

rule run_regional_my_str_gwas:
    input:
        "traits/phenotypes/{phenotype}.npy",
        'association/results/{phenotype}/my_str/time.stamp',
        "traits/subset_rin_phenotypes/{phenotype}.npy",
        f"str_imputed/runs/{str_imputation_run_name}/vcfs/annotated_strs/chr{{chr}}.vcf.gz",
        f"str_imputed/runs/{str_imputation_run_name}/vcfs/annotated_strs/chr{{chr}}.vcf.gz.tbi"
    output:
        "association/results/{phenotype}/my_str/batches/chr{chr}_{start}_{end}.tab"
    resources:
        time='12:00:00',
        mem_gb=9,
        threads=4
    shell:
        'association/my_regional_gwas.py strs {wildcards.phenotype} '
        '--region {wildcards.chr}:{wildcards.start}-{wildcards.end} '
        f'--imputation-run-name {str_imputation_run_name} '

rule concatenate_my_str_gwas:
    input:
        lambda wildcards:
            [f'association/results/{wildcards.phenotype}/my_str/batches/chr{region}.tab' for
             region in regions(int(1e7))]
    output:
        protected('association/results/{phenotype}/my_str/results.tab')
    resources:
        time='04:00:00'
    run:
        concatenate_csvs(output[0], input)

rule prep_plink_input:
    input:
        'traits/shared_covars/shared_covars.npy',
        'traits/shared_covars/covar_names.txt',
        'traits/subset_rin_phenotypes/{phenotype}.npy',
        'traits/phenotypes/{phenotype}_covar_names.txt'
    output:
        'association/results/{phenotype}/plink_snp/input/rin_phenotype_and_covars.tab'
    resources:
        time='00:30:00'
    shell:
        'association/prep_plink_input.py {wildcards.phenotype}'

rule timestamp_plink_run:
    output:
        'association/results/{phenotype}/plink_snp/time.stamp'
    resources:
        time='00:05:00'
    shell:
        'touch association/results/{wildcards.phenotype}/plink_snp/time.stamp'

rule run_plink_gwas:
    input:
        'association/results/{phenotype}/plink_snp/input/rin_phenotype_and_covars.tab',
        'association/results/{phenotype}/plink_snp/time.stamp'
    output:
        'association/results/{phenotype}/plink_snp/chrs/chr{chrom}/plink2.rin_{phenotype}.glm.linear.done'
    resources:
        time='47:30:00',
        threads=28,
        mem=110
    shell:
        'PHENOTYPE={wildcards.phenotype} CHROM={wildcards.chrom} association/plink_association.sh'

rule concatenate_plink_gwas:
    input:
        lambda wildcards:
            [f'association/results/{wildcards.phenotype}/plink_snp/chrs/chr{chrom}/plink2.rin_{wildcards.phenotype}.glm.linear.done' for
             chrom in range(1, 23)]
    output:
        protected('association/results/{phenotype}/plink_snp/results.tab')
    resources:
        time='04:00:00'
    run:
        concatenate_csvs(output[0], input)

rule prep_conditional_input:
    output:
        'association/results/{phenotype}/conditional_inputs/chr{chr}_STR{STRs,.*}__ISNP{imputed_SNPs,.*}__ASNP.npy',
        'association/results/{phenotype}/conditional_inputs/chr{chr}_STR{STRs,.*}__ISNP{imputed_SNPs,.*}__ASNP_varnames.txt',
        'association/results/{phenotype}/conditional_inputs/chr{chr}_STR{STRs,.*}__ISNP{imputed_SNPs,.*}__ASNP_README.txt'
    resources:
        time='00:30:00'
    run:
        command = 'association/prep_conditional_inputs.py {wildcards.phenotype} {wildcards.chr} '
        if wildcards.STRs:
            command += f'--imputation-run-name {str_imputation_run_name} --STRs ' 
            command += wildcards.STRs.replace('_', ' ') + ' '
        if wildcards.imputed_SNPs:
            command += '--imputed-SNPs '
            command += wildcards.imputed_SNPs.replace('_', ' ') + ' '
        shell(command)

rule run_conditional_my_str_gwas:
    input:
        "traits/phenotypes/{phenotype}.npy",
        "traits/subset_rin_phenotypes/{phenotype}.npy",
        f"str_imputed/runs/{str_imputation_run_name}/vcfs/annotated_strs/chr{{chr}}.vcf.gz",
        f"str_imputed/runs/{str_imputation_run_name}/vcfs/annotated_strs/chr{{chr}}.vcf.gz.tbi",
        'association/results/{phenotype}/conditional_inputs/chr{chr}_STR{STRs}__ISNP{imputed_SNPs}__ASNP.npy',
    output:
        'association/results/{phenotype}/my_str_conditional/chr{chr}_{start}_{end}_STR{STRs,.*}__ISNP{imputed_SNPs,.*}__ASNP.tab'
    resources:
        time='12:00:00',
        mem_gb=9,
        threads=4
    shell:
        'association/my_regional_gwas.py strs {wildcards.phenotype} '
        '--region {wildcards.chr}:{wildcards.start}-{wildcards.end} '
        f'--imputation-run-name {str_imputation_run_name} '
        '--conditional STR{wildcards.STRs}__ISNP{wildcards.imputed_SNPs}__ASNP'

rule prep_conditional_plink_input:
    input:
        'association/results/{phenotype}/conditional_inputs/chr{chr}_STR{STRs}__ISNP{imputed_SNPs}__ASNP.npy',
        'association/results/{phenotype}/conditional_inputs/chr{chr}_STR{STRs}__ISNP{imputed_SNPs}__ASNP_varnames.txt',
        'traits/shared_covars/shared_covars.npy',
        'traits/shared_covars/covar_names.txt',
        'traits/subset_rin_phenotypes/{phenotype}.npy',
        'traits/phenotypes/{phenotype}_covar_names.txt'
    output:
        'association/results/{phenotype}/conditional_inputs/chr{chr}_STR{STRs,.*}__ISNP{imputed_SNPs,.*}__ASNP_plink.tab'
    resources:
        time='00:30:00'
    shell:
        'association/prep_plink_input.py {wildcards.phenotype} --conditional chr{wildcards.chr}_STR{wildcards.STRs}__ISNP{wildcards.imputed_SNPs}__ASNP'

rule run_conditional_plink_gwas:
    input:
        'association/results/{phenotype}/conditional_inputs/chr{chrom}_STR{STRs}__ISNP{ISNPs}__ASNP_plink.tab'
    output:
        'association/results/{phenotype}/plink_snp_conditional/chr{chrom}_{start}_{end}_STR{STRs,.*}__ISNP{ISNPs,.*}__ASNP/plink2.rin_{phenotype}.glm.linear.done'
    resources:
        time='24:00:00',
        mem=110,
        threads=28
    shell:
        'PHENOTYPE={wildcards.phenotype} CHROM={wildcards.chrom} '
        'CONDITIONAL=STR{wildcards.STRs}__ISNP{wildcards.ISNPs}__ASNP '
        'START={wildcards.start} END={wildcards.end} '
        'association/plink_association.sh'

INTERACTIVE_MANHATTAN_GWAS_THRESHOLD = 0.00005

def subset_gwas_results(
        input_file,
        output_file,
        p_val_col,
        threshold = INTERACTIVE_MANHATTAN_GWAS_THRESHOLD):
    shell(
        f"head -n 1 {input_file} > {output_file}.temp && "
        "awk '{{ if ($" + str(p_val_col) + " < " + str(threshold) + ") {{ print }} }}' "
        f"{input_file} >> {output_file}.temp && "
        f"mv {output_file}.temp {output_file} "
    )


rule subset_my_gwas_results:
    input:
        'association/results/{phenotype}/my_{gwas_type}/{chr_prefix}results.tab'
    output:
        'association/plots/input/{phenotype}/my_{gwas_type}_{chr_prefix,.*}results.tab'
    resources:
        time='01:00:00'
    run:
        subset_gwas_results(
             f"association/results/{wildcards.phenotype}/my_{wildcards.gwas_type}/{wildcards.chr_prefix}results.tab",
             f"association/plots/input/{wildcards.phenotype}/my_{wildcards.gwas_type}_{wildcards.chr_prefix}results.tab",
             5
         )

rule subset_plink_gwas_results:
    input:
        'association/results/{phenotype}/plink_snp/results.tab'
    output:
        'association/plots/input/{phenotype}/plink_snp_results.tab'
    resources:
        time='01:00:00'
    run:
        subset_gwas_results(
             f"association/results/{wildcards.phenotype}/plink_snp/results.tab",
             f"association/plots/input/{wildcards.phenotype}/plink_snp_results.tab",
             14
        )

rule append_mfi_to_subsetted_plink_results:
    input:
        'association/plots/input/{phenotype}/plink_snp_results.tab'
    output:
        'association/plots/input/{phenotype}/plink_snp_results_with_mfi.npy'
    resources:
        time='01:00:00'
    shell:
        'association/append_mfi_to_plink_snp.py {input[0]}'

rule subset_conditional_my_str_gwas_results:
    input:
        'association/results/{phenotype}/my_str_conditional/{condition}.tab'
    output:
        'association/plots/input/{phenotype}/my_str_conditional_{condition}_results.tab'
    resources:
        time='01:00:00'
    run:
        subset_gwas_results(
             f'association/results/{wildcards.phenotype}/my_str_conditional/{wildcards.condition}.tab',
             f'association/plots/input/{wildcards.phenotype}/my_str_conditional_{wildcards.condition}_results.tab',
             5
         )

rule subset_conditional_plink_gwas_results:
    input:
        'association/results/{phenotype}/plink_snp_conditional/{condition}/plink2.rin_{phenotype}.glm.linear.done'
    output:
        'association/plots/input/{phenotype}/plink_snp_conditional_{condition}_results.tab'
    resources:
        time='01:00:00'
    run:
        subset_gwas_results(
             f'association/results/{wildcards.phenotype}/plink_snp_conditional/{wildcards.condition}/plink2.rin_{wildcards.phenotype}.glm.linear.done',
             f'association/plots/input/{wildcards.phenotype}/plink_snp_conditional_{wildcards.condition}_results.tab',
             14
         )

rule append_mfi_to_subsetted_conditional_plink_results:
    input:
        'association/plots/input/{phenotype}/plink_snp_conditional_{condition}_results.tab'
    output:
        'association/plots/input/{phenotype}/plink_snp_conditional_{condition}_results_with_mfi.npy'
    resources:
        time='01:00:00'
    shell:
        'association/append_mfi_to_plink_snp.py {input[0]}'

rule compare_my_to_plink_gwas:
    input:
        "traits/phenotypes/height_unit.txt",
        'association/plots/input/height/my_imputed_snp_chr21_results.tab',
        'association/plots/input/height/plink_snp_results_with_mfi.npy'
    output:
        'association/plots/height_my_imputed_snp_vs_plink.html'
    resources:
        time='00:30:00',
        mem=50
    shell:
        'association/interactive_manhattan_plots.py --my-plink-comparison'

rule interactive_manhattan:
    input:
        "traits/phenotypes/{phenotype}_unit.txt",
        'association/plots/input/{phenotype}/my_str_results.tab',
        'association/plots/input/{phenotype}/plink_snp_results_with_mfi.npy'
    output:
        'association/plots/{phenotype}_interactive_manhattan.html'
    resources:
        time='00:30:00',
        mem=50
    shell:
        'association/interactive_manhattan_plots.py --phenotype {wildcards.phenotype}'

rule interactive_manhattan_conditional:
    input:
        "traits/phenotypes/{phenotype}_unit.txt",
        'association/plots/input/{phenotype}/plink_snp_conditional_{condition}_results_with_mfi.npy',
        'association/plots/input/{phenotype}/my_str_conditional_{condition}_results.tab'
    output:
        'association/plots/{phenotype}_interactive_manhattan_{condition}.html'
    resources:
        time='00:30:00',
        mem=50
    shell:
        'association/interactive_manhattan_plots.py --phenotype {wildcards.phenotype} '
        '--condition {wildcards.condition} '

checkpoint clump_results:
    input:
        'association/results/{phenotype}/my_str/results.tab',
        'association/results/{phenotype}/plink_snp/results.tab'
    output:
        protected('finemapping/signal_clumps/{phenotype}.tab'),
        protected('finemapping/signal_clumps/{phenotype}_README.txt')
    resources:
        time='00:30:00'
    shell:
        'finemapping/clump.py {wildcards.phenotype}'

def run_finemap_time(wildcards, attempt):
    if attempt == 1:
        return '04:00:00'
    else:
        return '47:30:00'

def run_finemap_memory(wildcards, attempt):
    if attempt == 1:
        return 2
    else:
        return 10

def run_finemap_threads(wildcards, attempt):
    if attempt == 1:
        return 1
    else:
        return 5

rule run_finemap:
    input:
        'association/results/{phenotype}/my_str/results.tab',
        'association/results/{phenotype}/plink_snp/results.tab',
        'sample_qc/runs/{phenotype}/combined_unrelated.sample'
    output:
        protected('finemapping/finemap_results/{phenotype}/{chrom}_{start}_{end}/README.txt'),
        protected('finemapping/finemap_results/{phenotype}/{chrom}_{start}_{end}/finemap_output.snp'),
        protected('finemapping/finemap_results/{phenotype}/{chrom}_{start}_{end}/finemap_output.config')
    resources:
        mem_gb = run_finemap_memory,
        time = run_finemap_time,
        threads = run_finemap_threads
    shell:
        'finemapping/prep_and_run_finemap.py '
        '{wildcards.phenotype} {wildcards.chrom} {wildcards.start} '
        '{wildcards.end} '
        f'{str_imputation_run_name} '

def collect_finemap_target_files(wildcards):
    phenotype = wildcards.phenotype
    files = []
    with checkpoints.clump_results.get(**wildcards).output[0].open() as signal_file:
        reader = csv.reader(signal_file, delimiter='\t')
        next(reader) # skip header
        for line in reader:
            files.append(
                    f'finemapping/finemap_results/{phenotype}/{line[0]}_{line[1]}_{line[2]}/finemap_output.snp'
            )
    # TODO generalize this to other genotypes
    # Remove this region which won't finemap 
    for f in (
            'finemapping/finemap_results/height/6_25164325_33472053/finemap_output.snp',
            # TODO this next one is temp
            'finemapping/finemap_results/height/3_46342530_53729895/finemap_output.snp'
        ):
        files.remove(f)
    return files

rule run_finemap_phenotype:
    input:
        collect_finemap_target_files
    output:
        'finemapping/finemap_results/.{phenotype}_all_signals'
    resources:
        time='00:01:00'
    shell:
        'touch finemapping/finemap_results/.{wildcards.phenotype}_all_signals'

rule summarize_finemap_output:
    input:
        collect_finemap_target_files
    output:
        'finemapping/finemap_results/{phenotype}/summary/summary.txt'
    resources:
        time='00:30:00'
    shell:
        'finemapping/summarize_finemap_output.py {wildcards.phenotype} --signals {input}'

rule interactive_manhattan_finemap:
    input:
        "traits/phenotypes/{phenotype}_unit.txt",
        'association/plots/input/{phenotype}/my_str_results.tab',
        'association/plots/input/{phenotype}/plink_snp_results_with_mfi.npy',
        finemap=collect_finemap_target_files
    output:
        'association/plots/{phenotype}_interactive_manhattan_FINEMAP.html'
    resources:
        time='00:30:00',
        mem=50
    shell:
        'association/interactive_manhattan_plots.py --phenotype {wildcards.phenotype} --finemap-signals {input.finemap}'

